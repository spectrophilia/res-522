/**
 * Display encoder measurements from the wheels of the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  threading: false,
}

import Display from "lib/Display.lf"
import AngleToDistance from "lib/Encoders.lf"

main reactor {
  display = new Display()
  D = new AngleToDistance()

  timer t(0, 1s)

  reaction(t) -> D.trigger {=
    lf_set(D.trigger, true);
  =}

  reaction(startup) -> display.line0 {=
    lf_set(display.line0, "ENCODERS:");
  =}

  reaction(D.left_distance) -> display.line1 {=
    static char buf[17];
    snprintf(buf, 17, "L: %f", D.left_distance->value);
    lf_set(display.line1, buf);
  =}
  
  reaction(D.right_distance) -> display.line2 {=
    static char buf[17];
    snprintf(buf, 17, "R: %f", D.right_distance->value);
    lf_set(display.line2, buf); 
  =}
}
