/**
 * Template for robot driving lab exercises.
 * This template just periodically switches between a
 * STOPPED and a DRIVING mode, updating the LCD display
 * on each change of mode.
 */
target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  threading: false,
}

import Display from "lib/Display.lf"
import Motors from "lib/Motors.lf"

reactor Robot {
  input drive:bool       // Toggle mode.
  output notify:string   // Notify of mode change.
  
  reaction(startup) -> notify {=
    lf_set(notify, "INIT");
  =}

  initial mode STOPPED {
    reaction(drive) -> DRIVING, notify {=
      if (drive->value) {
        lf_set_mode(DRIVING);
        lf_set(notify, "DRIVING");
      }
    =}
  }

  mode DRIVING {
    reaction(drive) -> STOPPED, notify {=
      if (!drive->value) {
        lf_set_mode(STOPPED);
        lf_set(notify, "STOPPED");
      }
    =}
  }
}

main reactor {
  timer t(0, 2 sec)
  state drive:bool = false
  robot = new Robot()
  display = new Display()
  m = new Motors()

  reaction(t) -> robot.drive, m.left_power, m.right_power {=
      lf_set(robot.drive, self->drive);
      if (self->drive){
        lf_set(m.right_power, 0.1f);
        lf_set(m.left_power, 0.1f);
        }
      else {
        lf_set(m.right_power, 0.0f);
        lf_set(m.left_power, 0.0f);
        }
      // Toggle the drive state variable for next time.
      self->drive = !self->drive;

  =}
  robot.notify -> display.line0;
}
